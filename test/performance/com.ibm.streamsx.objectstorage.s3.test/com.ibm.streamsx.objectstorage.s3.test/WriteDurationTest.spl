//
// ****************************************************************************
// * Copyright (C) 2017, International Business Machines Corporation          *
// * All rights reserved.                                                     *
// ****************************************************************************
//

namespace com.ibm.streamsx.objectstorage.s3.test;

use com.ibm.streamsx.objectstorage.s3::*;

/**
 * This application tests the time to write objects to Object Storage.
 * The test data is created in memory of an operator before starting the time measurement.
 * One tuple send to the S3ObjectStorageSink represents the object to be created on Object Storage if tuplesPerObject parameter is 1.
 */
public composite WriteDurationTest {

	param
		expression<rstring> $accessKeyID : getSubmissionTimeValue("os-access-key-id");
		expression<rstring> $secretAccessKey : getSubmissionTimeValue("os-secret-access-key");
		expression<rstring> $endpoint : getSubmissionTimeValue("os-endpoint", "s3-api.us-geo.objectstorage.softlayer.net");
		expression<rstring> $bucket : getSubmissionTimeValue("os-bucket");
		expression<int32>   $numTuples : (int32)getSubmissionTimeValue("numTuples", "10");
		expression<int32>   $tupleSize : (int32)getSubmissionTimeValue("tupleSize", "1000000");
		expression<int32>   $tuplesPerObject: (int32)getSubmissionTimeValue("tuplesPerObject", "1");
		expression<int32>   $uploadWorkersNum: (int32)getSubmissionTimeValue("uploadWorkersNum", "10");

	graph

		stream<rstring result> DurationTest = WriteDurationTestCosComp() {
			param
				accessKeyID: $accessKeyID;
				secretAccessKey: $secretAccessKey;
				bucket: $bucket;
				endpoint: $endpoint;
				numTuples: $numTuples;
				tupleSize: $tupleSize;
				tuplesPerObject: $tuplesPerObject;
				uploadWorkersNum: $uploadWorkersNum;
		}

}

@threading(model=manual)
public composite WriteDurationTestCosComp (output PerfResult)
{
	param
		expression<rstring> $testName: "raw_cos";
		expression<rstring> $testId: "";
		expression<rstring> $accessKeyID;
		expression<rstring> $secretAccessKey;
		expression<rstring> $endpoint : "s3-api.us-geo.objectstorage.softlayer.net";
		expression<rstring> $bucket;
		expression<int32>   $numTuples : 10;
		expression<int32>   $tupleSize : 1000000;
		expression<int32>   $tuplesPerObject: 1;
		expression<int32>   $uploadWorkersNum: 10;

	graph

		/*
		 * Data Generator
		 */
		(stream<rstring data> DataString as O;
		 stream<rstring dummy> StartTimeStream as S) as DataGen = Custom() {
			logic
			state: {		
				mutable int32 numDataSet = 0;
				mutable boolean firstTuple = true;
			}
			onProcess: {
				mutable O otuple = {};
				for (int32 dataSetCounter in range($tupleSize)) {
					otuple.data += "x";
					numDataSet = dataSetCounter;
				}
				submit ({dummy=""}, S); // trigger start time
				for (int32 filenum in range($numTuples)) {
					if (isTraceable(Trace.info)) {
						appTrc(Trace.trace, "submit object "+(rstring)filenum);
					}
					submit (otuple, O); // send data
				}
				// prevent that final punct is sent
				while (true) {
					block(1.0);
				}
			}
			config placement: partitionColocation("PERFTEST");
		}	
		
		stream<rstring objectName, uint64 objectSize> ObjStSink = S3ObjectStorageSink(DataString) { 
			param
				accessKeyID : $accessKeyID;
				secretAccessKey : $secretAccessKey; 
				endpoint : $endpoint;
				bucket : $bucket;
				objectName: "test_data_%OBJECTNUM";
				tuplesPerObject: (int64)$tuplesPerObject;
				uploadWorkersNum: $uploadWorkersNum;
				protocol: cos;
				//vmArg: "-Xmx 4096m";
			config placement: partitionColocation("PERFTEST");
		}

		(stream<rstring result> PerfResult) as MeasureWrite = Custom(ObjStSink as I; StartTimeStream as S) {
			logic state: {
				mutable boolean isStarted = false;
				mutable timestamp startTimestamp;
				mutable int32 numTuples = 0;
				mutable uint64 numBytes = 0;
			}
			onTuple I: {
				numTuples++;
				numBytes += I.objectSize;
				if (($numTuples/$tuplesPerObject) == numTuples) {
					float64 duration = diffAsSecs(getTimestamp(),startTimestamp);
					float64 kb = (numBytes/1000ul);
					float64 rate = (kb/duration);
					printStringLn ("{'object_storage_test': '"+$testId+$testName+"_w"+(rstring)$uploadWorkersNum+"_tpo"+(rstring)$tuplesPerObject+"_tuplesize"+(rstring)$tupleSize+"_ntuples"+(rstring)$numTuples+"', 'num_objects': "+(rstring)numTuples+", 'num_bytes': "+(rstring)numBytes+", 'data_sent_KB_per_sec': "+(rstring)((int64)rate)+", 'duration_sec': "+(rstring)duration+"}");
					submit ({result="ok"}, PerfResult);
				}
			}
			onTuple S: { // time measurement start
				startTimestamp = getTimestamp();
			}
			config placement: partitionColocation("PERFTEST");
		}
		
	config restartable: false;
}

@threading(model=manual)
public composite WriteDurationTestS3aComp (output PerfResult)
{
	param
		expression<rstring> $testName: "raw_s3a";
		expression<rstring> $testId: "";	
		expression<rstring> $accessKeyID;
		expression<rstring> $secretAccessKey;
		expression<rstring> $endpoint : "s3-api.us-geo.objectstorage.softlayer.net";
		expression<rstring> $bucket;
		expression<int32>   $numTuples : 10;
		expression<int32>   $tupleSize : 1000000;
		expression<int32>   $tuplesPerObject: 1;
		expression<int32>   $uploadWorkersNum: 10;		

	graph

		/*
		 * Data Generator
		 */
		(stream<rstring data> DataString as O;
		 stream<rstring dummy> StartTimeStream as S) as DataGen = Custom() {
			logic
			state: {		
				mutable int32 numDataSet = 0;
				mutable boolean firstTuple = true;
			}
			onProcess: {
				mutable O otuple = {};
				for (int32 dataSetCounter in range($tupleSize)) {
					otuple.data += "x";
					numDataSet = dataSetCounter;
				}
				submit ({dummy=""}, S); // trigger start time
				for (int32 filenum in range($numTuples)) {
					if (isTraceable(Trace.info)) {
						appTrc(Trace.trace, "submit object "+(rstring)filenum);
					}
					submit (otuple, O); // send data
				}
				// prevent that final punct is sent
				while (true) {
					block(1.0);
				}
			}
			config placement: partitionColocation("PERFTEST");
		}	
		
		stream<rstring objectName, uint64 objectSize> ObjStSink = S3ObjectStorageSink(DataString) { 
			param
				accessKeyID : $accessKeyID;
				secretAccessKey : $secretAccessKey; 
				endpoint : $endpoint;
				bucket : $bucket;
				objectName: "test_data_%OBJECTNUM";
				tuplesPerObject: (int64)$tuplesPerObject;
				uploadWorkersNum: $uploadWorkersNum;
				protocol: s3a;
				//vmArg: "-Xmx 4096m";
			config placement: partitionColocation("PERFTEST");
		}

		(stream<rstring result> PerfResult) as MeasureWrite = Custom(ObjStSink as I; StartTimeStream as S) {
			logic state: {
				mutable boolean isStarted = false;
				mutable timestamp startTimestamp;
				mutable int32 numTuples = 0;
				mutable uint64 numBytes = 0;
			}
			onTuple I: {
				numTuples++;
				numBytes += I.objectSize;
				if (($numTuples/$tuplesPerObject) == numTuples) {
					float64 duration = diffAsSecs(getTimestamp(),startTimestamp);
					float64 kb = (numBytes/1000ul);
					float64 rate = (kb/duration);
					printStringLn ("{'object_storage_test': '"+$testId+$testName+"_w"+(rstring)$uploadWorkersNum+"_tpo"+(rstring)$tuplesPerObject+"_tuplesize"+(rstring)$tupleSize+"_ntuples"+(rstring)$numTuples+"', 'num_objects': "+(rstring)numTuples+", 'num_bytes': "+(rstring)numBytes+", 'data_sent_KB_per_sec': "+(rstring)((int64)rate)+", 'duration_sec': "+(rstring)duration+"}");
					submit ({result="ok"}, PerfResult);
				}
			}
			onTuple S: { // time measurement start
				startTimestamp = getTimestamp();
			}
			config placement: partitionColocation("PERFTEST");
		}
		
	config restartable: false;
}
