//
// ****************************************************************************
// * Copyright (C) 2017, International Business Machines Corporation          *
// * All rights reserved.                                                     *
// ****************************************************************************
//

namespace com.ibm.streamsx.objectstorage.swift.test;

use com.ibm.streamsx.objectstorage.swift::*;

/**
 * This sample application tests the time to write files to Object Storage
 * and the time to read files from Object Storage.
 * The test data is created in memory of an operator before starting the time measurement.
 * One tuple send to the SwiftSink represents the file to be created on Object Storage.
 * When all files have been created, then the read performance measurement starts and
 * all files are read from Object Storage and tuples are discarded.
 */
@threading(model=manual)
composite PeformanceTest {

	param
		expression<rstring> $objectStorageUserID:    getSubmissionTimeValue("ObjectStorage-UserId");
		expression<rstring> $objectStoragePassword:  getSubmissionTimeValue("ObjectStorage-Password");
		expression<rstring> $objectStorageProjectID: getSubmissionTimeValue("ObjectStorage-ProjectId");

		expression<rstring> $objectStorageServiceName: getSubmissionTimeValue("ObjectStorage-ServiceName");
		expression<rstring> $objectStorageContainer: getSubmissionTimeValue("ObjectStorage-Container", "streams-test");
		expression<rstring> $objectStorageAccessPoint: getSubmissionTimeValue("ObjectStorage-AccessPoint", "dal.objectstorage.open.softlayer.com");
		expression<int32> $numChannels: (int32) getSubmissionTimeValue("numChannels", "1");

	graph
	
		stream<rstring name> ConfigFile = DirectoryScan()   {
		            param
		                directory:  getThisToolkitDir()+"/etc/";
						pattern: "test_config.csv$";
			config placement: partitionColocation("TEST-CONFIG");
		}
	
		/*
		 * Test config files
		 * Specifies number of files and file size per test sequence
		 */	
		stream<int32 numFiles, int32 numBytes> CfgStream = FileSource(ConfigFile) {
			param
				parsing : permissive;
			config placement: partitionColocation("TEST-CONFIG");
		}
	
		@parallel(width = $numChannels, broadcast=[CfgStream])
		() as Tester = Test(CfgStream) {
			param
				objectStorageUserID : $objectStorageUserID;
				objectStoragePassword : $objectStoragePassword; 
				objectStorageProjectID : $objectStorageProjectID;
				objectStorageServiceName: $objectStorageServiceName;
				objectStorageContainer : $objectStorageContainer + (rstring)getChannel();
				objectStorageAccessPoint: $objectStorageAccessPoint;
			
		}		

	config restartable: false;
}

composite Test (input CfgStream)
{
	param
		expression<rstring> $objectStorageUserID;
		expression<rstring> $objectStoragePassword;
		expression<rstring> $objectStorageProjectID;
		expression<rstring> $objectStorageServiceName;
		expression<rstring> $objectStorageContainer;
		expression<rstring> $objectStorageAccessPoint;
		

	graph
	
		/*
		 * Setup bucket and clean-up
		 */
		(
		stream<I> CfgStream1
		) as PrepareTest = Custom(CfgStream as I) {
			logic
			state: {
				mutable boolean res = initialize($objectStorageUserID, $objectStoragePassword, $objectStorageProjectID); // init Swift client
				mutable boolean isFirstTuple = true;
			}
			onTuple I: {
				if (isFirstTuple) {
					isFirstTuple = false;
					printStringLn ("["+(rstring)getChannel()+"]"+"Setup ...");										
					createContainer($objectStorageContainer);
					deleteAllObjects($objectStorageContainer);
				}
				submit (I, CfgStream1);
			}
			config placement: partitionColocation("TEST-CONFIG");
		}

		(
		stream<rstring objectName, rstring data> DataString as O	
		) as DataGen = Custom(CfgStream1 as I) {
			logic
			state: {
				mutable O otuple = {};				
				mutable int32 numDataSet = 0;
				mutable int32 outRolling = 0;
				mutable boolean firstTuple = true;
				mutable list<O> dataSets = [];
				mutable int32 activeDataSetIdx = 0;
			}
			onTuple I: {
				for (int32 dataSetCounter in range(I.numBytes)) {
					otuple.data += "x";
					numDataSet = dataSetCounter;
				}
				outRolling++;

				for (int32 filenum in range(I.numFiles)) {
					otuple.objectName= "testFile_" + (rstring)outRolling + "_" + (rstring)filenum + ".bin";
					submit (otuple, O); // send data
					submit(Sys.WindowMarker, O); // time measurement end
				}
				otuple.data = "";
			}
			config placement: partitionColocation("PERFTEST-WRITE"+(rstring)getChannel()), partitionExlocation("TEST-CONFIG");
		}	
		
		stream<rstring objectName, uint64 filesize> ObjStSink = SwiftObjectStorageSink(DataString) { 
			param
				userID: $objectStorageUserID;
				password: $objectStoragePassword;
				projectID: $objectStorageProjectID;
				serviceName: $objectStorageServiceName;
				accessPoint: $objectStorageAccessPoint;
				objectNameAttribute: objectName;
				container: $objectStorageContainer;
				vmArg : "-Xmx4096m";
				genOpenObjPunct: true;
			config placement: partitionColocation("PERFTEST-WRITE"+(rstring)getChannel());
		}

		stream<uint64 fileSize, float64 writeDuration> WriteRawStat = Custom(ObjStSink as I) {
			logic state: {
				mutable boolean isStarted = false;
				mutable timestamp startTimestamp;
			}
			onTuple I: {
				float64 duration = diffAsSecs(getTimestamp(),startTimestamp);
				printStringLn ("["+(rstring)getChannel()+"]"+"[WRITE] bytes=" + (rstring)filesize + ", duration="+(rstring)duration);
				submit({fileSize = filesize, writeDuration = duration}, WriteRawStat);
				isStarted = false;				
			}
			onPunct I: { // time measurement start
				if (currentPunct() == Sys.WindowMarker) {
					if (!isStarted) { // start time measurement
						isStarted = true;
						startTimestamp = getTimestamp();
					} 
				}
			}
			config placement: partitionColocation("PERFTEST-WRITE"+(rstring)getChannel());
		}				
		
		
		stream<rstring objectName> ObjNames = Functor(ObjStSink) {}

		stream<rstring data> ObjStSource = SwiftObjectStorageSource(ObjNames) { 
			param
				userID: $objectStorageUserID;
				password: $objectStoragePassword;
				projectID: $objectStorageProjectID;
				serviceName: $objectStorageServiceName;
				accessPoint: $objectStorageAccessPoint;
				container: $objectStorageContainer;
				vmArg : "-Xmx4096m";
				genOpenObjPunct: true;
			config placement: partitionColocation("PERFTEST"+(rstring)getChannel());
		}
	
		(stream<rstring command> CmdStream) as MeasureRead = Custom(ObjStSource as I) {
			logic state: {
				mutable boolean isStarted = false;
				mutable timestamp startTimestamp;
			}
			onTuple I: {
				if (isStarted) { // test nature allows to measure on the first tuple
					float64 duration = diffAsSecs(getTimestamp(),startTimestamp);					
					printStringLn ("["+(rstring)getChannel()+"]"+"[READ] bytes=" + (rstring)length(data) + ", duration="+(rstring)duration);
					// reset
					isStarted = false;
					submit({command="end"}, CmdStream);
				}
			}
			onPunct I: { // time measurement start
				if (!isStarted) { // start time measurement
					isStarted = true;
					startTimestamp = getTimestamp();
				}
			}
			config placement: partitionColocation("PERFTEST-READ"+(rstring)getChannel());
		}
		
		() as CleanupTest = Custom(CmdStream as I) {
			logic
			state: {
				mutable boolean res = initialize($objectStorageUserID, $objectStoragePassword, $objectStorageProjectID); // init Swift client
			}
			onTuple I: {
				printStringLn ("["+(rstring)getChannel()+"]"+"Clean-up ...");
				// deleteAllObjects($bucket);
			}
			onPunct I: {
				if(currentPunct() == Sys.FinalMarker) {
					printStringLn ("["+(rstring)getChannel()+"]"+"FINAL PUNCT");
					// deleteBucket($bucket);
				}
			}
			config placement: partitionColocation("TEST-CONFIG");
		}
	
	
	config restartable: false;
}
