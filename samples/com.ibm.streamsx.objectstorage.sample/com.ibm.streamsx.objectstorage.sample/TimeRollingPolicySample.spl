namespace com.ibm.streamsx.objectstorage.sample;

use com.ibm.streamsx.objectstorage::ObjectStorageSink;

/**
 * The sample demonstrates object creation 
 * on time basis
 */
public composite TimeRollingPolicySample {
	param
		expression<rstring> $objectStorageURI: getSubmissionTimeValue("objectStorageURI"); // swift example: "swift2d://TimeRollingPolicyTest.ObjectStorage-dp/"; 
		expression<rstring> $objectStorageUser: getSubmissionTimeValue("userID"); 
		expression<rstring> $objectStoragePassword: getSubmissionTimeValue("password");
		expression<rstring> $objectStorageProjectID: getSubmissionTimeValue("projectID");
		expression<rstring> $objectName: getSubmissionTimeValue("objectName", "every60Sec %TIME"); 
		expression<float64> $timePerObject: (float64)getSubmissionTimeValue("timePerObject", "60.0");
		expression<rstring> $endpoint: getSubmissionTimeValue("endpoint", "dal.objectstorage.open.softlayer.com");
		
	type
		Tweet_t = rstring username, rstring tweet, uint64 tweettime;
	
	graph
	
		/* Generate tweets with a changing value for every tweet */
		stream<Tweet_t> Tweets = Beacon()
		{
			param				
				period: 0.05;
			output
				Tweets : username = "John", tweet = "Nice weather today! "
					+(rstring) IterationCount(), tweettime = 1048298232ul + IterationCount() ;
		}
	
		/** Format tweets */
		stream<rstring tweetStr> FormattedTweets = Functor(Tweets) {
            output
                FormattedTweets : tweetStr = (rstring)Tweets;
        }
	
		/** Write tweets to the object storage
		 *  Close object every "timePerObject" 
		 */
		() as osSink = ObjectStorageSink(FormattedTweets) {
			param
				objectStorageURI : $objectStorageURI;
				objectStorageUser : $objectStorageUser;
				objectStoragePassword : $objectStoragePassword;
				objectStorageProjectID : $objectStorageProjectID;
				objectName : $objectName;
				endpoint : $endpoint;
				timePerObject : $timePerObject;				
	}
}
