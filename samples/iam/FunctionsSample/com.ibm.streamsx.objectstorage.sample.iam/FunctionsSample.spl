//
// ****************************************************************************
// * Copyright (C) 2017, International Business Machines Corporation          *
// * All rights reserved.                                                     *
// ****************************************************************************
//

namespace com.ibm.streamsx.objectstorage.sample.iam;

use com.ibm.streamsx.objectstorage::*;
use com.ibm.streamsx.objectstorage.s3::*;
use com.ibm.streamsx.objectstorage::ObjectStorageSink;
use com.ibm.streamsx.objectstorage::ObjectStorageSinkOut_t;

/**
 * This sample application shows the usage of the Operator and functions to access Object Storage.
 * A bucket is created, sample data read and written as several small objects to Object Storage, then read from Object Storage.
 * Finally the objects and the bucket is deleted.
 */
@threading(model=manual)
composite FunctionsSample {

	param
		expression<rstring> $IAMApiKey: getSubmissionTimeValue("os-iam-api-key", "");
		expression<rstring> $IAMServiceInstanceId: getSubmissionTimeValue("os-iam-service-instance", "");
		expression<rstring> $IAMTokenEndpoint: getSubmissionTimeValue("os-iam-token-endpoint", "https://iam.bluemix.net/oidc/token");
		expression<rstring> $endpoint : getSubmissionTimeValue("os-endpoint", "s3-api.us-geo.objectstorage.softlayer.net");
		expression<rstring> $bucket : getSubmissionTimeValue("os-bucket", "streams-sample");

	graph

		stream<rstring result> SampleRes = FunctionsSampleIAM() {
			param
				IAMApiKey: $IAMApiKey;
				IAMServiceInstanceId: $IAMServiceInstanceId;
				IAMTokenEndpoint: $IAMTokenEndpoint;
				bucket: $bucket;
				endpoint: $endpoint;
		}	
}

public composite FunctionsSampleIAM (output SampleResult) {
	param
		expression<rstring> $IAMApiKey: "";
		expression<rstring> $IAMServiceInstanceId: "";
		expression<rstring> $IAMTokenEndpoint: "https://iam.bluemix.net/oidc/token";
		expression<rstring> $bucket;
		expression<rstring> $endpoint: "s3-api.us-geo.objectstorage.softlayer.net";
		expression<rstring> $objectName: "output_%TIME.txt";

	type
		DataType = tuple<rstring objectName, rstring data>;
		S3ObjectStorageSourceIn_t = tuple<rstring objectName>;
		S3ObjectStorageSinkOut_t = tuple<rstring objectName, uint64 size>;

	graph

		(stream<rstring data> Lines as O) as DataGen = Custom() {
			logic state: {
				mutable boolean res = initialize_iam($IAMApiKey, $IAMServiceInstanceId, $endpoint); // init S3 client
				mutable boolean res1 = createBucket($bucket);
			} 
			onProcess: {
				submit ({data="first line"}, O);
				submit ({data="second line"}, O);
				submit ({data="third line"}, O);
				while (true) {
					block(1.0);
				}
			}
		}	
		
		stream<S3ObjectStorageSinkOut_t> ObjStSink = ObjectStorageSink(Lines as I) {
			param
				IAMApiKey: $IAMApiKey;
				IAMServiceInstanceId: $IAMServiceInstanceId;
				IAMTokenEndpoint: $IAMTokenEndpoint;
				objectStorageURI: getObjectStorageURI($bucket);
				endpoint : $endpoint;
				tuplesPerObject: 1l;
				timeFormat: "yyyy-MM-dd'T'HHmmss";		
				objectName: $objectName;
		}

		(stream<S3ObjectStorageSourceIn_t> ObjToRead as O) as SampleReader = Custom(ObjStSink as I) {
			logic
			onTuple I: {
				printStringLn("Object with name '" + I.objectName + "' of size '" + (rstring)I.size + "' has been created.");
				printStringLn("read object " + I.objectName);
				submit ({objectName=I.objectName}, O);
			}
		}		

		stream<rstring line> ObjStSource = ObjectStorageSource(ObjToRead as I) {
			param
				IAMApiKey: $IAMApiKey;
				IAMServiceInstanceId: $IAMServiceInstanceId; 
				IAMTokenEndpoint: $IAMTokenEndpoint;
				objectStorageURI: getObjectStorageURI($bucket);								
				endpoint : $endpoint;
		}
		
		stream<rstring result> SampleResult = Custom(ObjStSource as I) {
			logic
			state: {
				mutable boolean res = initialize_iam($IAMApiKey, $IAMServiceInstanceId, $endpoint); // init S3 client
				mutable int32 count = 0;
			}
			onTuple I: {
				printStringLn ((rstring)I); // dump the tuple
				count++;
				if (3 == count) {
					printStringLn ("list objects");
					mutable list<rstring> objects = listObjects($bucket);
					for (rstring name in objects) {
						printStringLn ("delete object: " + name);
						deleteObject(name, $bucket);
					}
					printStringLn ("deleteBucket " + $bucket);
					deleteBucket($bucket);
					submit ({result="ok"}, SampleResult);
				}
			}
		}


}
