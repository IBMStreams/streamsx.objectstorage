//
// ****************************************************************************
// * Copyright (C) 2017, International Business Machines Corporation          *
// * All rights reserved.                                                     *
// ****************************************************************************
//

namespace com.ibm.streamsx.objectstorage.sample.iam;

use com.ibm.streamsx.objectstorage::*;
use com.ibm.streamsx.objectstorage.s3::*;
use com.ibm.streamsx.objectstorage::ObjectStorageSink;
use com.ibm.streamsx.objectstorage::ObjectStorageSinkOut_t;

/**
 * This sample application shows the usage of the operators and functions to access Object Storage.
 * A bucket is created, sample data read and written as several small objects to Object Storage, then read from Object Storage.
 * Finally the objects and the bucket is deleted.
 * 
 * Additional purpose of the sample is to demonstrate how to configure the operators and functions with IAM-authentication type.
 *
 * Either set `cos.creds` containing COS credentials JSON in `cos` application configuration or set `credentials` parameter.
 * 
 * @param credentials;
 * Parameter to set IBM COS service credentials in JSON
 * Overwrites the property `cos.creds` in `cos` application configuration.
 * 
 * @param endpoint
 * Optional parameter to set an endpoint to connect to COS, default: s3.us.cloud-object-storage.appdomain.cloud
 *
 * @param bucket 
 * Set a bucket name that belongs to your COS user
 */
@threading(model=manual)
composite FunctionsSample {

	param
		expression<rstring> $credentials: getSubmissionTimeValue("os-credentials", "");
		expression<rstring> $endpoint : getSubmissionTimeValue("os-endpoint", "s3.us.cloud-object-storage.appdomain.cloud");
		expression<rstring> $bucket : getSubmissionTimeValue("os-bucket", "streams-sample");

	graph

		stream<rstring result> SampleRes = FunctionsSampleIAM() {
			param
				credentials: $credentials;
				bucket: $bucket;
				endpoint: $endpoint;
		}	
}

/**
 * Used by FunctionsSample main composite and test application
 *
 * @param credentials;
 * Parameter to set IBM COS service credentials in JSON
 * Overwrites the property `cos.creds` in `cos` application configuration.
 * 
 * @param endpoint
 * Optional parameter to set an endpoint to connect to COS, default: s3.us.cloud-object-storage.appdomain.cloud
 *
 * @param bucket 
 * Set a bucket name that belongs to your COS user
 * 
 * @param objectName
 * Object name used by Sink operator
 *
 * @output SampleResult
 * This stream is evaluated by the tester.
 */
public composite FunctionsSampleIAM (output SampleResult) {
	param
		expression<rstring> $credentials: "";
		expression<rstring> $bucket;
		expression<rstring> $endpoint: "s3.us.cloud-object-storage.appdomain.cloud";
		expression<rstring> $objectName: "output_%TIME.txt";

	type
		DataType = tuple<rstring objectName, rstring data>;
		S3ObjectStorageSourceIn_t = tuple<rstring objectName>;
		S3ObjectStorageSinkOut_t = tuple<rstring objectName, uint64 size>;

	graph

		(stream<rstring data> Lines as O) as DataGen = Custom() {
			logic state: {
				mutable boolean res = initialize_cos($credentials, $endpoint); // init S3 client
				mutable boolean res1 = createBucket($bucket);
			} 
			onProcess: {
				submit ({data="first line"}, O);
				submit ({data="second line"}, O);
				submit ({data="third line"}, O);
				while (true) {
					block(1.0);
				}
			}
		}	
		
		stream<S3ObjectStorageSinkOut_t> ObjStSink = ObjectStorageSink(Lines as I) {
			param
				credentials: $credentials;
				objectStorageURI: getObjectStorageURI($bucket);
				endpoint : $endpoint;
				tuplesPerObject: 1l;
				timeFormat: "yyyy-MM-dd'T'HHmmss";		
				objectName: $objectName;
		}

		(stream<S3ObjectStorageSourceIn_t> ObjToRead as O) as SampleReader = Custom(ObjStSink as I) {
			logic
			onTuple I: {
				printStringLn("Object with name '" + I.objectName + "' of size '" + (rstring)I.size + "' has been created.");
				printStringLn("read object " + I.objectName);
				submit ({objectName=I.objectName}, O);
			}
		}		

		stream<rstring line> ObjStSource = ObjectStorageSource(ObjToRead as I) {
			param
				credentials: $credentials;
				objectStorageURI: getObjectStorageURI($bucket);								
				endpoint : $endpoint;
		}
		
		stream<rstring result> SampleResult = Custom(ObjStSource as I) {
			logic
			state: {
				mutable boolean res = initialize_cos($credentials, $endpoint); // init S3 client
				mutable int32 count = 0;
			}
			onTuple I: {
				printStringLn ((rstring)I); // dump the tuple
				count++;
				if (3 == count) {
					printStringLn ("list objects");
					mutable list<rstring> objects = listObjects($bucket);
					for (rstring name in objects) {
						printStringLn ("delete object: " + name);
						deleteObject(name, $bucket);
					}
					printStringLn ("deleteBucket " + $bucket);
					deleteBucket($bucket);
					submit ({result="ok"}, SampleResult);
				}
			}
		}


}
