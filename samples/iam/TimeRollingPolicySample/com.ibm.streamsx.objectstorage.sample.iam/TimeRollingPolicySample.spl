/*
** Copyright (C) 2018  International Business Machines Corporation
** All Rights Reserved
*/
namespace com.ibm.streamsx.objectstorage.sample.iam;

use com.ibm.streamsx.objectstorage::ObjectStorageSink;


/**
 * The sample demonstrates object creation 
 * on time basis
 * 
 * Additional purpose of the sample is to demonstrate how to configure the operators and functions with IAM-authentication type.
 *
 * Either set `cos.creds` containing COS credentials JSON in `cos` application configuration or set `credentials` parameter.
 * 
 * @param credentials;
 * Parameter to set IBM COS service credentials in JSON
 * Overwrites the property `cos.creds` in `cos` application configuration.
 * 
 * @param endpoint
 * Optional parameter to set an endpoint to connect to COS, default: s3.us.cloud-object-storage.appdomain.cloud
 *
 * @param objectStorageURI 
 * Set a bucket name and protocol in URI in 'cos://bucket/' or 's3a://bucket/' format
 * 
 * @param objectName
 * Object name used by Sink operator
 *
 * @param timePerObject
 * Time to keep an object name before closing
 */
public composite TimeRollingPolicySample {
	param
		expression<rstring> $credentials: getSubmissionTimeValue("os-credentials", "");
		expression<rstring> $objectStorageURI: getSubmissionTimeValue("os-uri");
		expression<rstring> $endpoint : getSubmissionTimeValue("os-endpoint", "s3.us.cloud-object-storage.appdomain.cloud");
	
		expression<rstring> $objectName: getSubmissionTimeValue("objectName", "every30Sec_%TIME"); 
		expression<float64> $timePerObject: (float64)getSubmissionTimeValue("timePerObject", "30.0");

	graph

		stream<rstring result> SampleRes = TimeRollingPolicySampleIAM() {
			param
				credentials: $credentials;
				objectStorageURI: $objectStorageURI;
				endpoint: $endpoint;
				objectName: $objectName;
				timePerObject: $timePerObject;
		}	
}

/**
 * Used by TimeRollingPolicySample main composite and test application
 *
 * @param credentials;
 * Parameter to set IBM COS service credentials in JSON
 * Overwrites the property `cos.creds` in `cos` application configuration.
 * 
 * @param endpoint
 * Optional parameter to set an endpoint to connect to COS, default: s3.us.cloud-object-storage.appdomain.cloud
 *
 * @param objectStorageURI 
 * Set a bucket name and protocol in URI in 'cos://bucket/' or 's3a://bucket/' format
 * 
 * @param objectName
 * Object name used by Sink operator
 *
 * @param timePerObject
 * Time to keep an object name before closing
 *
 * @output SampleResult
 * This stream is evaluated by the tester.
 */
public composite TimeRollingPolicySampleIAM (output SampleResult) {
	param
		expression<rstring> $credentials: "";
		expression<rstring> $objectStorageURI;
		expression<rstring> $endpoint: "s3.us.cloud-object-storage.appdomain.cloud";
	
		expression<rstring> $objectName; 
		expression<float64> $timePerObject;

	graph
		
		stream<rstring tweetStr> Tweets = TweetsGenerator() {}	
		
		/** Write tweets to the object storage
		 *  Close object every "timePerObject" 
		 */
		stream<rstring objectName, uint64 objectSize> ObjStSink = ObjectStorageSink(Tweets) {
			param
				credentials: $credentials;
				objectStorageURI: $objectStorageURI;								
				objectName : $objectName;
				endpoint : $endpoint;
				timePerObject : $timePerObject;	
		}
		
		stream<rstring result> SampleResult = Custom(ObjStSink as I) {
			logic 
			onTuple I: {
				submit ({result="ok"}, SampleResult);
			}
		}		
		
}

composite TweetsGenerator(output FormattedTweets) {
		
	type
		Tweet_t = rstring username, rstring tweet, uint64 tweettime;
	
	graph
	
		stream<Tweet_t> Tweets = Beacon() {
			param
				period: 0.5;
			output
				Tweets : username = "John", tweet = "Nice weather today! "
 					+(rstring) IterationCount(), tweettime = 1048298232ul + IterationCount() ;
		}

		stream<rstring tweetStr> FormattedTweets = Functor(Tweets) {
			output
				FormattedTweets : tweetStr = (rstring)Tweets;
		}
	
}
