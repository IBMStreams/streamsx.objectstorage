//
// ****************************************************************************
// * Copyright (C) 2017, International Business Machines Corporation          *
// * All rights reserved.                                                     *
// ****************************************************************************
//

namespace com.ibm.streamsx.objectstorage.swift.test;

use com.ibm.streamsx.objectstorage.swift::SwiftSource;
use com.ibm.streamsx.objectstorage.swift::SwiftSink;
use com.ibm.streamsx.objectstorage.swift::*;

/**
 * This sample application tests the time to write files to Object Storage
 * and the time to read files from Object Storage.
 * The test data is created in memory of an operator before starting the time measurement.
 * One tuple send to the SwiftSink represents the file to be created on Object Storage.
 * When all files have been created, then the read performance measurement starts and
 * all files are read from Object Storage and tuples are discarded.
 */
@threading(model=manual)
composite Main {

	param
		expression<rstring> $objectStorageUserID:    getSubmissionTimeValue("ObjectStorage-UserId");
		expression<rstring> $objectStoragePassword:  getSubmissionTimeValue("ObjectStorage-Password");
		expression<rstring> $objectStorageProjectID: getSubmissionTimeValue("ObjectStorage-ProjectId");
		expression<rstring> $objectStorageContainer: getSubmissionTimeValue("ObjectStorage-Container");
		expression<rstring> $objectStorageAccessPoint: "lon.objectstorage.open.softlayer.com";

	graph

		/*
		 * Test config files
		 * Specifies number of files and file size per test sequence
		 */	
		stream<int32 numFiles, int32 numBytes> CfgStream = FileSource() {
			param
				file : getThisToolkitDir()+"/etc/test_config.csv";
				parsing : permissive;
		}	

		/*
		 * Data Generator
		 */
		stream<rstring objectName, rstring data> DataString as O = Custom(CfgStream as I) {
			logic
			state: {
				mutable map<int32, rstring> dataMap;
				mutable rstring testData = "";
				mutable int32 numDataSets = 0;
			}
			onTuple I: {
				for (int32 fileId in range(I.numFiles)) {
					for (int32 dataSetCounter in range(I.numBytes)) {
						testData += "x";
						numDataSets = dataSetCounter;
					}
					dataMap[fileId] = testData;
					testData = "";
				}
				printStringLn ("----------------------------------------");
				printStringLn ("numFiles="+(rstring)I.numFiles);
				printStringLn ("fileSize="+(rstring)I.numBytes);
				// Start the WRITE FILE measurement
				submit(Sys.WindowMarker, O); // time measurement start
				for (int32 fileIndex in dataMap) {
					submit ({objectName=(rstring) fileIndex, data=dataMap[fileIndex]}, O);
				}
				submit(Sys.WindowMarker, O); // time measurement end
				// reset
				clearM(dataMap);
				numDataSets = 0;
				testData = "";
			}
			config placement: partitionColocation("PERFTEST");
		}
		
		stream<rstring objectName> ObjStSink = SwiftSink(DataString) { 
			param
				objectStorageUserID : $objectStorageUserID;
				objectStoragePassword : $objectStoragePassword;
				objectStorageProjectID : $objectStorageProjectID;
				objectStorageContainer : $objectStorageContainer;
				objectStorageAccessPoint: $objectStorageAccessPoint;
			config placement: partitionColocation("PERFTEST");                 
		}

		(stream<rstring objectName> FilesToRead as O) as MeasureWrite = Custom(ObjStSink as I) {
			logic state: {
				mutable boolean isStarted = false;
				mutable timestamp startTimestamp;
				mutable int32 numFiles = 0;
				mutable list<rstring> fileNames;
			}
			onTuple I: {
				insertM(fileNames, I.objectName, numFiles);
				numFiles++;
			}
			onPunct I: {
				if(currentPunct() == Sys.WindowMarker) {
					if (!isStarted) {
						isStarted = true;
						startTimestamp = getTimestamp();
					}
					else {
						mutable timestamp duration = diffAsSecs(getTimestamp(),startTimestamp);
						printStringLn ("[WRITE] duration="+(rstring)duration);
						printStringLn ("[WRITE] numFiles="+(rstring)numFiles);
						
						// Start the READ FILE measurement
						submit(Sys.WindowMarker, O); // time measurement start
						for (rstring name in fileNames) {
							submit ({objectName=name}, O);
						}
						submit(Sys.WindowMarker, O); // time measurement end
						// reset
						clearM(fileNames);
						numFiles = 0;
						isStarted = false;				
					}
				}
			}
			config placement: partitionColocation("PERFTEST");			
		}		

		stream<rstring objectName, rstring data> ObjStSource = SwiftSource(FilesToRead) { 
			param
				objectStorageUserID : $objectStorageUserID;
				objectStoragePassword : $objectStoragePassword;
				objectStorageProjectID : $objectStorageProjectID;
				objectStorageContainer : $objectStorageContainer;
				objectStorageAccessPoint: $objectStorageAccessPoint;
			config placement: partitionColocation("PERFTEST");                 
		}
		
		() as MeasureRead = Custom(ObjStSource as I) {
			logic state: {
				mutable boolean isStarted = false;
				mutable timestamp startTimestamp;
				mutable int32 numFiles = 0;
				mutable list<rstring> fileNames;
			}
			onTuple I: {
				insertM(fileNames, I.objectName, numFiles);
				numFiles++;
			}
			onPunct I: {
				if(currentPunct() == Sys.WindowMarker) {
					if (!isStarted) {
						isStarted = true;
						startTimestamp = getTimestamp();
					}
					else {
						mutable timestamp duration = diffAsSecs(getTimestamp(),startTimestamp);
						printStringLn ("[READ] duration="+(rstring)duration);
						printStringLn ("[READ] numFiles="+(rstring)numFiles);
						
						// clean-up - delete objects
						mutable rstring authKey = authenticate($objectStorageUserID, $objectStoragePassword, $objectStorageProjectID, $objectStorageAccessPoint);
						for (rstring name in fileNames) {
							deleteObject(name, $objectStorageContainer, $objectStorageProjectID, $objectStorageAccessPoint, authKey);
						}
						// reset
						clearM(fileNames);
						numFiles = 0;
						isStarted = false;
					}
				}
			}
			config placement: partitionColocation("PERFTEST");
		}
		
	config restartable: false;
}
